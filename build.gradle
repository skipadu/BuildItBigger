// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.google.guava:guava:22.0'
        classpath 'com.android.tools.build:gradle:3.1.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven {
            url "https://maven.google.com"
        }
    }
}

task startAppengine {
    doFirst {
        println "appengine starting..."
    }

    dependsOn ":backend:appengineStart"

    doLast {
        println "###--- appengine started ---###"
    }
}

task runFreeTests {
    mustRunAfter startAppengine
    doFirst {
        println "Starting to run tests for Free..."
    }

    dependsOn = [":app:connectedFreeDebugAndroidTest", ":app:testFreeDebugUnitTest", ":app:testFreeReleaseUnitTest"]

    doLast {
        println "Ran all tests for Free."
    }
}

task runPaidTests {
    mustRunAfter startAppengine
    doFirst {
        println "Starting to run tests for Paid..."
    }

    dependsOn = [":app:connectedPaidDebugAndroidTest", ":app:testPaidDebugUnitTest", ":app:testPaidReleaseUnitTest"]

    doLast {
        println "Ran all tests for Paid."
    }
}

task runTestsWithGCEOn {
    group "My own tasks"
    description "Stops the appengine first to make sure it is not already running. Then starts appengine. Runs connected tests for free and paid. And finally stops the appengine."

    dependsOn = ["startAppengine", "runFreeTests", "runPaidTests"]

    finalizedBy "stopAppengine"

    doFirst {
        println "Starting to run tests"
    }

    doLast {
        println "All tests done"
    }
}

task stopAppengine {
    doFirst {
        println "appengine stopping..."
    }

    dependsOn ":backend:appengineStop"

    doLast {
        println "###--- appengine stopped ---###"
    }
}
